def isReleaseBuild = !project.hasProperty('devBuild')

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
if (isReleaseBuild) {
    apply plugin: 'com.google.firebase.firebase-crash'
    apply plugin: 'com.google.firebase.firebase-perf'
}
apply plugin: 'com.github.triplet.play'
build.dependsOn 'updateReleasePlayVersion'

apply from: '../quality/quality.gradle'

android {
    compileSdkVersion 'android-O'
    buildToolsVersion '26.0.0-rc2' // Note: remember to update travis.yml and upload.sh versions too

    defaultConfig {
        manifestPlaceholders = [appName: '@string/app_name']
        applicationId 'com.supercilex.robotscouter'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1 // The version code is updated on each Travis build
        versionName '1.1.2'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = file('keystore.properties')
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: 'Robot Scouter DEBUG']
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    track = 'alpha'
    serviceAccountEmail = 'google-play-auto-publisher@robot-scouter-app.iam.gserviceaccount.com'
    jsonFile = file('google-play-auto-publisher.json')
}

if (isReleaseBuild) {
    android.applicationVariants.all { variant ->
        variant.preBuild.doLast {
            variant.mergedFlavor.versionName = "${android.defaultConfig.versionName}.$variant.versionCode"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    // Support libraries
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:support-vector-drawable:$supportLibVersion"
    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta1'

    // Firebase UI
    // Auth
//    compile 'com.firebaseui:firebase-ui-auth:2.0.0'
    compile 'com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-auth:0f7fc902c62fdc09e1c5812dd4dec2e6f5b4083d'
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    // Database
//    compile 'com.firebaseui:firebase-ui-database:2.0.0'
    compile 'com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-database:0f7fc902c62fdc09e1c5812dd4dec2e6f5b4083d'
    compile "com.google.firebase:firebase-database:$playServicesVersion"

    compile 'com.firebase:firebase-jobdispatcher:0.6.0'

    // Firebase core
    compile "com.google.firebase:firebase-core:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"
    compile "com.google.firebase:firebase-perf:$playServicesVersion"
    compile "com.google.firebase:firebase-invites:$playServicesVersion"
    compile "com.google.firebase:firebase-appindexing:$playServicesVersion"
    compile "com.google.firebase:firebase-config:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"

    // From Google-ish
    compile 'pub.devrel:easypermissions:0.3.1'
    compile 'com.github.bumptech.glide:glide:4.0.0-RC0'

    // Third party
    compile 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.10.0'

    // Apache Poi - to update, use https://github.com/SUPERCILEX/poi-on-android
    provided 'org.apache.poi:poi-ooxml:3.16-beta2' // Used for documentation

    // Leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
}

apply plugin: 'com.google.gms.google-services'
